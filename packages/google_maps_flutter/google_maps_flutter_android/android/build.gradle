group 'io.flutter.plugins.googlemaps'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    // Conditional for compatibility with AGP <4.2.
    if (project.android.hasProperty("namespace")) {
        namespace 'io.flutter.plugins.googlemaps'
    }
    compileSdk 34

    defaultConfig {
        minSdkVersion 20
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        checkAllWarnings true
        warningsAsErrors true
        disable 'AndroidGradlePluginVersion', 'InvalidPackage', 'GradleDependency'
    }

    dependencies {
        implementation "androidx.annotation:annotation:1.7.0"
        // Downgraded from 18.2.0 to 18.1.0 - for some reason, 18.2.0 introduces a bug where map camera update
        // callbacks can be delayed by up to a full second while dragging the map around;
        // In order for floating marker titles to work properly:
        // https://github.com/androidseb/flutter_map_floating_marker_titles
        // we're downgrading to 18.1.0 until there is a new play-services-maps library version fixing that bug.
        implementation 'com.google.android.gms:play-services-maps:18.2.0'
        androidTestImplementation 'androidx.test:runner:1.2.0'
        androidTestImplementation 'androidx.test:rules:1.4.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.mockito:mockito-core:5.1.1'
        testImplementation 'androidx.test:core:1.2.0'
        testImplementation "org.robolectric:robolectric:4.10.3"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
